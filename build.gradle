import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}


group 'xyz.oribuin'
version '1.0'
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    // Spigot
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    // PlaceholderAPI
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // Worldedit
    maven { url 'https://maven.enginehub.org/repo/' }
    // Vault
    maven { url = 'https://jitpack.io' }
}

dependencies {
    // Overlord language
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.0"

    // Spigot Stuff
    shadow 'org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT'
    shadow 'me.clip:placeholderapi:2.10.9'

    // Frameworks
    implementation 'dev.esophose:guiframework:1.1.0'

    // Plugin Hooks
    shadow 'com.sk89q.worldedit:worldedit-bukkit:7.1.0-SNAPSHOT'
    shadow 'com.github.MilkBowl:VaultAPI:1.7'

    // Database Stuff
    implementation 'com.zaxxer:HikariCP:3.4.5'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

shadowJar {
    mergeServiceFiles()
    archiveFileName = 'Skyblock-' + project.property("version") + '.jar'
    relocate('dev.rosewood.guiframework', 'xyz.oribuin.skyblock.libs.guiframework')
    relocate('org.jetbrains.kotlin', 'xyz.oribuin.skyblock.libs.kotlin')
}