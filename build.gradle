import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'xyz.oribuin'
version '1.0'
compileJava.options.encoding = 'UTF-8'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    // Spigot
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.rosewooddev.io/repository/public/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Overlord language
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.10"

    // Spigot Stuff
    compileOnly 'org.spigotmc:spigot:1.16.4'
    compileOnly 'me.clip:placeholderapi:2.10.9'

    // Frameworks
    implementation 'dev.rosewood:guiframework:1.1.1'

    // Plugin Hooks
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.1.0-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'

    // Misc
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation 'de.tr7zw:item-nbt-api:2.5.0'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

shadowJar {
    archiveFileName = 'Skyblock-' + project.property('version') + '.jar'
    archiveClassifier.set(null)

    relocate('org.jetbrains', 'xyz.oribuin.skyblock.libs.jetbrains')
    relocate('org.intellij', 'xyz.oribuin.skyblock.libs.intellij')
    relocate('net.wesjd.anvilgui', 'xyz.oribuin.skyblock.libs.anvilgui')
    relocate('kotlin', 'xyz.oribuin.skyblock.libs.kotlin')
    relocate('dev.rosewood.guiframework', 'xyz.oribuin.skyblock.libs.guiframework')
}