plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.30' apply false
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
}

configurations { childJars }

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group 'xyz.oribuin'
    version '1.0'
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()

        flatDir { dirs 'libs' }

        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://repo.rosewooddev.io/repository/public/' }
        maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://repo.codemc.org/repository/maven-public' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
    }

}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS-Wrapper')
    implementation project(':NMS-v1_16_R1')
    implementation project(':NMS-v1_16_R2')
    implementation project(':NMS-v1_16_R3')

    subprojects.each { childJars project(it.path) }
}

shadowJar {
    archiveClassifier.set(null)
    relocate('me.mattstudios', 'xyz.oribuin.skyblock.libs.mattstudios')
    relocate('xyz.oribuin.orilibrary', 'xyz.oribuin.skyblock.libs.orilib')
    relocate('kotlin', 'xyz.oribuin.skyblock.libs.kotlin')
    relocate('org.intellij', 'xyz.oribuin.skyblock.libs.intellij')
    relocate('org.jetbrains', 'xyz.oribuin.skyblock.libs.jetbrains')
}

jar {
    dependsOn configurations.childJars

    from { configurations.childJars.collect { zipTree(it) } }

}

build.dependsOn shadowJar